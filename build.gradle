buildscript {
    apply from: 'https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-repositories.gradle', to: buildscript
    apply from: 'https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-cgp-version.gradle'

    ext {
        javaTargetCompatibility = 8
        javaSourceCompatibility = 8
    }

    dependencies { classpath "com.blackduck.integration:common-gradle-plugin:${managedCgpVersion}" }
}

project.ext.moduleName = 'com.blackduck.integration.jenkins-common'
project.ext.javaUseAutoModuleName = 'true'

version = '1.0.0-SNAPSHOT'
description = 'Library for common Blackduck Jenkins plugin logic'

apply plugin: 'com.blackduck.integration.library'

final def resources = new File("${projectDir}/src/main/resources")
final def versionFile = new File(resources, "version.txt")
resources.mkdirs()
versionFile.delete()
versionFile << "version=" << version << "\n" << "builddate=" << new Date().format('yyyy-MM-dd')

repositories {
    // Included to resolve Jenkins and Jenkins plugin dependencies
    maven { url 'https://repo.jenkins-ci.org/releases/' }
}

dependencies {
    // These MUST stay in lock step with any $PRODUCT-common you use with this
    // --rotte MAR 2020
    api 'com.blackduck.integration:integration-rest:11.0.0'
    api 'com.blackduck.integration:phone-home-client:7.0.0'

    // added by gradle jpi plugin to our plugins
    implementation 'com.google.code.findbugs:annotations:3.0.0'
    implementation 'javax.servlet:javax.servlet-api:3.1.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'

    // Unfortunately, without making jenkins-common a fully fledged Jenkins plugin we cannot enforce dependencies on specific Jenkins versions
    // or Jenkins plugin versions in Jenkins plugins that depend on this library.
    // Jenkins plugins that depend on this library should therefore version those dependencies compatibly with the versions below:
    // --rotte DEC 2019

    implementation 'org.jenkins-ci.main:jenkins-core:2.377'

    implementation 'org.jenkins-ci.plugins:credentials:1139.veb_9579fca_33b_@jar'
    implementation 'org.jenkins-ci.plugins:credentials:1139.veb_9579fca_33b_'
    implementation 'org.jenkins-ci.plugins:plain-credentials:1.8@jar'
    implementation 'org.jenkins-ci.plugins:plain-credentials:1.8'
    implementation 'org.jenkins-ci.plugins.workflow:workflow-support:827.v7ef666c4d65c@jar'
    implementation 'org.jenkins-ci.plugins.workflow:workflow-support:827.v7ef666c4d65c'
    implementation 'org.jenkins-ci.plugins.workflow:workflow-api:1188.v0016b_4f29881@jar'
    implementation 'org.jenkins-ci.plugins.workflow:workflow-api:1188.v0016b_4f29881'
}
